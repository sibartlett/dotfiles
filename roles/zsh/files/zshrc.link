#!/usr/bin/env zsh
# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/dotfiles

PURE_CMD_MAX_EXEC_TIME=1

# setup antibody
source <(antibody init)
antibody bundle < "$ZSH/roles/zsh/files/bundles.txt"

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load everything but the completion files
for file in ${config_files:#*/completion.zsh}; do
  source "$file"
done

autoload -Uz compinit
typeset -i updated_at=$(date +'%j' -r ~/.zcompdump 2>/dev/null || stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
if [ $(date +'%j') != $updated_at ]; then
  compinit
else
  compinit -C
fi

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

unset config_files updated_at

# use .localrc for secrets that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi
